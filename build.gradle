import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipOutputStream

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

version = mod_version
group = mod_group_id\

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://parchmentmc.org/docs/getting-started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'

    // When true, this property will have all Eclipse/IntelliJ IDEA run configurations run the "prepareX" task for the given run configuration before launching the game.
    // In most cases, it is not necessary to enable.
    // enableEclipsePrepareRuns = true
    // enableIdeaPrepareRuns = true

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    // When true, this property will add the folder name of all declared run configurations to generated IDE run configurations.
    // The folder name can be set on a run configuration using the "folderName" property.
    // By default, the folder name of a run configuration is the name of the Gradle project containing it.
    // generateRunFolders = true

    // This property enables access transformers for use in development.
    // They will be applied to the Minecraft artifact.
    // The access transformer file can be anywhere in the project.
    // However, it must be at "META-INF/accesstransformer.cfg" in the final mod jar to be loaded by Forge.
    // This default location is a best practice to automatically put the file in the right place in the final jar.
    // See https://docs.minecraftforge.net/en/latest/advanced/accesstransformers/ for more information.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    def os = org.gradle.internal.os.OperatingSystem.current()
    def arch = System.getProperty("os.arch").contains("64") ? "x86_64" : "x86"

    def nativeSubdir =
            os.isWindows() ? "windows/${arch}" :
                    os.isLinux()   ? "linux/${arch}"   :
                            os.isMacOsX()  ? "osx/${arch}"     :
                                    null

    if (nativeSubdir == null) {
        throw new GradleException("Unsupported OS for natives")
    }

    def nativePath = file("src/main/resources/natives/${nativeSubdir}").absolutePath

    runs {

        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')
            workingDirectory project.file('run')

            jvmArgs "-Djava.library.path=${nativePath}"

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }

        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // flatDir {
    //     dir 'libs'
    // }
    maven {
        url "https://cursemaven.com"
    }
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    minecraftLibrary 'de.javagl:jgltf-model:2.0.4'
    implementation fg.deobf("curse.maven:distant-horizons-508933:6387680")


    minecraftLibrary("io.github.spair:imgui-java-binding:${project.imguiVersion}") {
        exclude group: "org.lwjgl"
    }
    minecraftLibrary("io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}") {
        exclude group: "org.lwjgl"
    }

    runtimeOnly "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    runtimeOnly "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"

    shadow("io.github.spair:imgui-java-binding:${project.imguiVersion}") {
        exclude group: "org.lwjgl"
    }
    shadow("io.github.spair:imgui-java-lwjgl3:${project.imguiVersion}") {
        exclude group: "org.lwjgl"
    }
    shadow "io.github.spair:imgui-java-natives-windows:${project.imguiVersion}"
    shadow "io.github.spair:imgui-java-natives-linux:${project.imguiVersion}"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}


mixin {
    // MixinGradle Settings
    add sourceSets.main, 'mixins.noodle_engine.refmap.json'
    config 'noodle_engine.mixins.json'
}

shadowJar {
    configurations = [project.configurations.shadow]
//    classifier 'shadow-dev'
    dependencies {
        exclude(dependency('org.lwjgl:lwjgl'))
        exclude(dependency('org.lwjgl:lwjgl-glfw'))
        exclude(dependency('org.lwjgl:lwjgl-opengl'))
    }
}


jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

task moveFiles {
    doLast {
        // Input and output files based on the shadowJar and jar archive file names
        def inputFile = file("$buildDir/libs/" + shadowJar.archiveFileName.get())
        def outputFile = file("$buildDir/libs/" + jar.archiveFileName.get())

        // Temporary file for the updated output
        def tempFile = file("$buildDir/libs/" + "output_temp.zip")

        if (inputFile.exists() && outputFile.exists()) {
            def sourceZip = new ZipFile(inputFile)
            def destinationZip = new ZipFile(outputFile)
            def zipOut = new ZipOutputStream(new FileOutputStream(tempFile))

            // Copy existing contents of the destination ZIP to the temporary file
            destinationZip.entries().each { entry ->
                zipOut.putNextEntry(new ZipEntry(entry.getName()))
                zipOut.write(destinationZip.getInputStream(entry).readAllBytes())
                zipOut.closeEntry()
            }

            // Copy new files from the source ZIP that aren't already in the destination ZIP
            sourceZip.entries().each { entry ->
                if (destinationZip.getEntry(entry.getName()) == null) {
                    zipOut.putNextEntry(new ZipEntry(entry.getName()))
                    zipOut.write(sourceZip.getInputStream(entry).readAllBytes())
                    zipOut.closeEntry()
                } else {
                    println "File already exists in destination ZIP: ${entry.getName()}. Skipping."
                }
            }

            // Close all streams
            zipOut.close()
            sourceZip.close()
            destinationZip.close()

            // Replace the original destination file with the new one
            if (outputFile.exists()) {
                outputFile.delete() // Delete the old output file if it exists
            }
            tempFile.renameTo(outputFile) // Rename temp file to the destination file
            inputFile.delete() // Delete the source file (inputFile) after moving
        } else {
            println "Input or output ZIP file not found."
        }
    }
}

build {
    dependsOn shadowJar
    finalizedBy moveFiles
}

task extractImguiNativesToResources {
    group = "build"
    description = "Extract ImGui native binaries from Gradle dependencies into resources/natives/... for packaging"

    doLast {
        // Map file extensions/patterns to their resource subdirectory
        def nativeMappings = [
                { String name -> name.endsWith(".dll") } : "windows/x86_64",
                { String name -> name.endsWith("32.dll") } : "windows/x86",
                { String name -> name.endsWith(".so") } : "linux/x86_64",
                { String name -> name.endsWith("32.so") } : "linux/x86",
                { String name -> name.endsWith(".dylib") } : "osx/x86_64",
        ]

        configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            if (artifact.name.startsWith("imgui-java-natives")) {
                println "Extracting from dependency: ${artifact.file.name}"

                ZipFile zip = new ZipFile(artifact.file)
                zip.entries().each { entry ->
                    if (!entry.isDirectory()) {
                        def entryName = entry.name
                        def fileName = entryName.substring(entryName.lastIndexOf('/') + 1)

                        // Determine which OS/arch subdir this native belongs to based on filename
                        def subdir = null
                        if (fileName.endsWith(".dll")) {
                            subdir = fileName.contains("32") ? "windows/x86" : "windows/x86_64"
                        } else if (fileName.endsWith(".so")) {
                            subdir = fileName.contains("32") ? "linux/x86" : "linux/x86_64"
                        } else if (fileName.endsWith(".dylib")) {
                            subdir = fileName.contains("arm") ? "osx/arm64" : "osx/x86_64"
                        }

                        if (subdir != null) {
                            def destDir = file("src/main/resources/natives/${subdir}")
                            destDir.mkdirs()

                            def outFile = new File(destDir, fileName)
                            outFile.withOutputStream { osStream ->
                                zip.getInputStream(entry).withStream { it.transferTo(osStream) }
                            }
                            println "Copied $fileName -> $outFile"
                        }
                    }
                }
                zip.close()
            }
        }
    }
}

compileJava.dependsOn(extractImguiNativesToResources)